/*
 * Licensed to Systerel under one or more contributor license
 * agreements. See the NOTICE file distributed with this work
 * for additional information regarding copyright ownership.
 * Systerel licenses this file to you under the Apache
 * License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License. You may obtain
 * a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

IMPLEMENTATION
    channel_mgr_i

REFINES
    channel_mgr_r

SEES
    constants,
    constants_statuscodes_bs,
    time_reference_bs

IMPORTS
    channel_mgr_1,
    channel_mgr_it,
    channel_mgr_bs

CONCRETE_VARIABLES
    all_channel_closing, /* Indicates all secure channels are in closing state (S2OPC toolkit clear case) */
    all_client_only_channel_closing /* Indicates all secure channels as a client are in closing state (S2OPC client activity clear) */

INVARIANT
    all_channel_closing : BOOL &
    all_client_only_channel_closing : BOOL

INITIALISATION
    all_channel_closing := FALSE;
    all_client_only_channel_closing := FALSE

PROMOTES
    is_valid_channel_config_idx,
    is_valid_endpoint_config_idx,
    server_get_endpoint_config,
    is_client_channel,
    get_channel_info,
    get_all_channel_info,
    get_connected_channel,
    is_disconnecting_channel,
    is_connected_channel,
    get_SecurityPolicy,
    channel_do_nothing,
    get_connection_time

LOCAL_OPERATIONS

    l_close_secure_channel(p_channel, p_statusCode, p_close_ctx) =
    PRE
        p_channel : t_channel_i &
        p_channel : t_channel &
        p_statusCode : t_StatusCode_i &
        p_statusCode : t_StatusCode &
        p_close_ctx : t_application_context_i &
        /* Setting a close context is only possible if the channel is not already disconnecting */
        (p_close_ctx /= c_no_application_context =>
            a_config(p_channel) /: s_cli_channel_disconnecting)
    THEN
        IF  p_channel : s_channel_connected &
            p_channel /: dom(a_endpoint) /* Client side only: */
        THEN
            s_cli_channel_disconnecting := s_cli_channel_disconnecting \/ {a_config(p_channel)} ||
            a_cli_channel_closing_ctx
            :(a_cli_channel_closing_ctx : t_channel_config_idx +-> t_application_context_i &
              (p_close_ctx /= c_no_application_context =>
                    a_cli_channel_closing_ctx = a_cli_channel_closing_ctx$0 \/  {a_config(p_channel) |-> p_close_ctx}) &
              (p_close_ctx = c_no_application_context =>
                    a_cli_channel_closing_ctx = a_cli_channel_closing_ctx$0))
        END
    END
    ;

    l_check_all_channel_lost =
    BEGIN
        channels_to_iterate,
        channels_iterated,
        all_channel_closing,
        all_client_only_channel_closing
        :(  channels_to_iterate <: t_channel &
            channels_iterated   <: t_channel &
            channels_to_iterate /\ channels_iterated = {} &
            channel_configs_to_iterate <: t_channel_config_idx &
            channel_configs_iterated   <: t_channel_config_idx &
            channel_configs_to_iterate /\ channel_configs_iterated = {} &
            all_channel_closing : BOOL &
            all_client_only_channel_closing : BOOL
         )
     END
     ;

     l_allowed_new_sc <-- l_is_new_sc_connection_allowed (is_one_sc_auto_closing) =
     PRE
         is_one_sc_auto_closing : BOOL
     THEN
        l_allowed_new_sc :=
        bool(card(s_cli_channel_connecting) + card(s_channel_connected) /= card(t_channel) & /* No slot remaining */
            (card(s_cli_channel_connecting) + card(s_channel_connected) < c_max_channels_connected
             or is_one_sc_auto_closing = TRUE)  /* we accept a connection after the maximum if a connection was released in exchange */)
     END

OPERATIONS

    /* Local operations */

    l_close_secure_channel(p_channel, p_statusCode, p_close_ctx) =
    VAR
        l_res,
        l_channel_conf,
        l_is_client_channel
    IN
        l_res, l_channel_conf <-- getall_channel_connected(p_channel);
        IF l_res = TRUE THEN
            l_is_client_channel <-- is_client_channel(p_channel);
            IF l_is_client_channel = TRUE
            THEN
                add_cli_channel_disconnecting(l_channel_conf, p_close_ctx)
            END
            ;
            finalize_close_secure_channel(p_channel, p_statusCode)
        END
    END
    ;

    l_check_all_channel_lost =
    VAR
        l_cli_con,
        l_any_channel_connected_or_connecting,
        l_continue,
        l_channel,
        l_config_idx
    IN
        IF  all_channel_closing = TRUE or all_client_only_channel_closing = TRUE THEN
            /* Connected channels */
            l_any_channel_connected_or_connecting := FALSE;
            l_continue <-- init_iter_channel;
            WHILE
                l_continue = TRUE &
                l_any_channel_connected_or_connecting = FALSE
            DO
                l_continue, l_channel <-- continue_iter_channel;
                l_cli_con <-- is_client_channel (l_channel);
                IF all_channel_closing = TRUE or (all_client_only_channel_closing = TRUE & l_cli_con = TRUE)
                THEN
                    l_any_channel_connected_or_connecting <-- is_channel_connected(l_channel)
                END
            INVARIANT
               channels_to_iterate <: t_channel &
               channels_iterated   <: t_channel &
               channels_to_iterate /\ channels_iterated = {} &
               channels_to_iterate \/ channels_iterated = t_channel
            VARIANT
               card(channels_to_iterate)
            END;
            /* Connecting channels */
            l_continue <-- init_iter_channel_config_idx;
            WHILE l_continue = TRUE & l_any_channel_connected_or_connecting = FALSE DO
                l_continue, l_config_idx <-- continue_iter_channel_config_idx;
                l_any_channel_connected_or_connecting <-- is_cli_channel_connecting(l_config_idx)
            INVARIANT
                channel_configs_to_iterate <: t_channel_config_idx &
                channel_configs_iterated   <: t_channel_config_idx &
                channel_configs_to_iterate /\ channel_configs_iterated = {} &
                channel_configs_to_iterate \/ channel_configs_iterated = t_channel_config_idx
            VARIANT
                card(channel_configs_to_iterate)
            END;
            IF  l_any_channel_connected_or_connecting = FALSE THEN
                last_connected_channel_lost(all_client_only_channel_closing);
                all_channel_closing := FALSE;
                all_client_only_channel_closing := FALSE
            END
        END
    END
    ;

    l_allowed_new_sc <-- l_is_new_sc_connection_allowed (is_one_sc_auto_closing) =
    VAR
        l_card_used,
        l_card_channel
    IN
        l_allowed_new_sc := FALSE;
        l_card_used <-- get_card_channel_used;
        l_card_channel <-- get_card_t_channel;
        /* It is possible to have no slot at all when channels are already disconnecting due to auto close, wait a channel slot to be freed */
        IF l_card_used /= l_card_channel THEN
            /* Check we keep at least one channel slot to allow auto close channel to be evaluated later in services layer.
            If we accept a connection and reach cardinal the secure channel layer will not accept any new connection and
            service layer will not be aware of next connections attempts to evaluate possible auto close.
            */
            IF l_card_used < c_max_channels_connected or is_one_sc_auto_closing = TRUE
            THEN
                l_allowed_new_sc := TRUE
            END
        END
    END
    ;

    /* Global operations */

    bres <-- cli_open_secure_channel (config_idx, reverse_endpoint_config_idx, is_one_sc_auto_closing) =
    VAR
        l_already_connecting,
        l_dom,
        l_channel,
        l_allowed_new_sc
    IN
        l_already_connecting <-- is_cli_channel_connecting(config_idx);
        IF l_already_connecting = TRUE THEN
            bres := TRUE /* Connection already requested: nothing to do */
        ELSE
            l_dom, l_channel <-- getall_config_inv(config_idx);
            IF  l_dom = FALSE THEN
                l_allowed_new_sc <-- l_is_new_sc_connection_allowed (is_one_sc_auto_closing);
                IF l_allowed_new_sc = TRUE
                THEN
                    add_cli_channel_connecting(config_idx);
                    prepare_cli_open_secure_channel(config_idx, reverse_endpoint_config_idx);
                    bres := TRUE
                ELSE
                    bres := FALSE
                END
            ELSE
                bres := FALSE
            END
        END
    END
    ;

    bres <-- srv_new_secure_channel (endpoint_config_idx, channel_config_idx, channel, is_one_sc_auto_closing) =
    VAR
        l_con,
        l_dom,
        l_channel,
        l_current_time,
        l_allowed_new_sc
    IN
        l_con <-- is_channel_connected(channel);
        l_dom, l_channel <-- getall_config_inv(channel_config_idx);
        l_allowed_new_sc <-- l_is_new_sc_connection_allowed (is_one_sc_auto_closing);
        IF  l_con = FALSE &
            l_dom = FALSE &
            l_allowed_new_sc = TRUE
        THEN
            l_current_time <-- get_current_TimeReference;
            add_channel_connected(channel, l_current_time);
            set_config(channel, channel_config_idx, c_reverse_endpoint_config_idx_indet);
            set_endpoint(channel, endpoint_config_idx);
            define_SecurityPolicy(channel);
            bres := TRUE
        ELSE
            bres := FALSE
        END
    END
    ;

    close_secure_channel(channel, statusCode) =
    BEGIN
        l_close_secure_channel(channel, statusCode, c_no_application_context)
    END
    ;

    bres <-- close_client_channel (p_config_idx, p_close_ctx) =
    VAR
        l_channel,
        l_config_idx,
        l_connected,
        l_connecting,
        l_disconnecting,
        l_cli_con,
        l_new_sc_disconnecting
    IN
        l_new_sc_disconnecting := FALSE;
        l_connecting <-- is_cli_channel_connecting (p_config_idx);
        l_disconnecting <-- is_disconnecting_channel (p_config_idx);
        l_channel <-- get_connected_channel (p_config_idx);
        l_connected <-- is_connected_channel (l_channel);
        IF l_disconnecting = FALSE
        THEN
            IF l_connected = TRUE
            THEN
                l_cli_con <-- is_client_channel (l_channel);
                IF l_cli_con = TRUE
                THEN
                    l_close_secure_channel(l_channel, e_sc_bad_secure_channel_closed, p_close_ctx);
                    l_new_sc_disconnecting := TRUE
                END
            ELSIF l_connecting = TRUE
            THEN
                add_cli_channel_disconnecting(p_config_idx, p_close_ctx);
                l_new_sc_disconnecting := TRUE
            END /* ELSE: channel already disconnected and not connecting neither */
        END /* ELSE: already disconnecting: nothing to do */
        ;
        bres := l_new_sc_disconnecting
    END
    ;

    bres <-- close_all_channel (p_clientOnly) =
    VAR
        l_continue,
        l_channel,
        l_config_idx,
        l_connected,
        l_connecting,
        l_cli_con,
        l_any_channel_closing_or_connecting
    IN
        l_any_channel_closing_or_connecting := FALSE;
        l_continue <-- init_iter_channel;
        /* Connected channels */
        WHILE l_continue = TRUE DO
            l_continue, l_channel <-- continue_iter_channel;
            l_connected <-- is_channel_connected(l_channel);
            l_cli_con <-- is_client_channel (l_channel);
            IF l_connected = TRUE &
                (p_clientOnly = FALSE or l_cli_con = TRUE)
            THEN
                l_any_channel_closing_or_connecting := TRUE;
                l_close_secure_channel(l_channel, e_sc_bad_secure_channel_closed, c_no_application_context)
            END
        INVARIANT
            channels_to_iterate <: t_channel &
            channels_iterated   <: t_channel &
            channels_to_iterate /\ channels_iterated = {} &
            channels_to_iterate \/ channels_iterated = t_channel
        VARIANT
            card(channels_to_iterate)
        END;
        /* Connecting channels */
        l_continue <-- init_iter_channel_config_idx;
        WHILE l_continue = TRUE DO
            l_continue, l_config_idx <-- continue_iter_channel_config_idx;
            l_connecting <-- is_cli_channel_connecting(l_config_idx);
            IF l_connecting = TRUE
            THEN
                l_any_channel_closing_or_connecting := TRUE;
                /* Set as disconnecting to be closed in case of set connected event: see cli_set_connected_channel */
                add_cli_channel_disconnecting(l_config_idx, c_no_application_context)
            END
        INVARIANT
            channel_configs_to_iterate <: t_channel_config_idx &
            channel_configs_iterated   <: t_channel_config_idx &
            channel_configs_to_iterate /\ channel_configs_iterated = {} &
            channel_configs_to_iterate \/ channel_configs_iterated = t_channel_config_idx
        VARIANT
            card(channel_configs_to_iterate)
        END;

        all_channel_closing := bool(p_clientOnly = FALSE & l_any_channel_closing_or_connecting = TRUE);
        all_client_only_channel_closing := bool(p_clientOnly = TRUE & l_any_channel_closing_or_connecting = TRUE);
        bres := l_any_channel_closing_or_connecting
    END
    ;

    channel_lost (channel) =
    VAR
        l_res,
        l_channel_conf,
        l_closing_ctx
    IN
        l_res, l_channel_conf <-- getall_channel_connected(channel);
        IF  l_res = TRUE THEN
            remove_channel_connected(channel);
            l_closing_ctx <-- remove_cli_channel_disconnecting(l_channel_conf);
            IF l_closing_ctx /= c_no_application_context
            THEN
                notify_client_connection_closed(l_channel_conf, l_closing_ctx)
            END
            ;
            reset_config(channel);
            reset_endpoint(channel);
            reset_SecurityPolicy(channel);
            l_check_all_channel_lost
        END
    END
    ;

    bres <-- cli_set_connected_channel (config_idx, reverse_endpoint_config_idx, channel) =
    VAR
        l_is_channel_connecting,
        l_is_channel_disconnecting,
        l_is_channel_connected,
        l_current_time,
        l_closing_ctx
    IN
        l_is_channel_connecting <-- is_cli_channel_connecting(config_idx);
        l_is_channel_disconnecting <-- is_disconnecting_channel(config_idx);
        l_is_channel_connected  <-- is_channel_connected(channel);
        IF  l_is_channel_connecting = TRUE &
            l_is_channel_connected = FALSE
        THEN
            remove_cli_channel_connecting(config_idx);
            l_current_time <-- get_current_TimeReference;
            add_channel_connected(channel, l_current_time);
            set_config(channel, config_idx, reverse_endpoint_config_idx);
            define_SecurityPolicy(channel);
            /* If channel was tagged for disconnection, close it now it is connected */
            IF l_is_channel_disconnecting = TRUE
            THEN
                l_closing_ctx <-- remove_cli_channel_disconnecting (config_idx);
                l_close_secure_channel(channel, e_sc_bad_secure_channel_closed, l_closing_ctx)
            END
            ;
            bres := TRUE
        ELSE
            bres := FALSE
        END
    END
    ;

    bres <-- cli_set_connection_timeout_channel (config_idx) =
    VAR
        l_res
    IN
        l_res <-- is_cli_channel_connecting(config_idx);
        IF  l_res = TRUE THEN
            remove_cli_channel_connecting(config_idx);
            l_check_all_channel_lost;
            bres := TRUE
        ELSE
            bres := FALSE
        END
    END
    ;

    p_auto_closed_active <-- is_auto_close_channel_active =
    VAR
        l_card_used,
        l_card_channel
    IN
        l_card_used <-- get_card_channel_used;
        l_card_channel <-- get_card_t_channel;
        p_auto_closed_active := bool(l_card_used >= c_max_channels_connected)
    END
    ;

    set_create_session_locked (p_channel) =
    VAR
        l_end_lock_target_timeref
    IN
        l_end_lock_target_timeref <-- get_target_TimeReference (c_channel_lock_create_session_delay);
        set_create_session_locked_1 (p_channel, l_end_lock_target_timeref)
    END
    ;

    update_create_session_locked (p_channel) =
    VAR
        l_current_timeref,
        l_target_timeref,
        l_target_not_reached
    IN
        l_target_timeref <-- get_create_session_locked_1 (p_channel);
        IF l_target_timeref /= c_timeref_indet
        THEN
            l_current_timeref <-- get_current_TimeReference;
            l_target_not_reached <-- is_less_than_TimeReference(l_current_timeref, l_target_timeref);
            IF l_target_not_reached = FALSE
            THEN
                set_create_session_locked_1 (p_channel, c_timeref_indet)
            END
        END
    END
    ;

    p_session_locked <-- is_create_session_locked (p_channel) =
    VAR
        l_target_timeref
    IN
        l_target_timeref <-- get_create_session_locked_1 (p_channel);
        p_session_locked := bool(l_target_timeref /= c_timeref_indet)
    END

END
