/*
 * Licensed to Systerel under one or more contributor license
 * agreements. See the NOTICE file distributed with this work
 * for additional information regarding copyright ownership.
 * Systerel licenses this file to you under the Apache
 * License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License. You may obtain
 * a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

REFINEMENT
    address_space_r
REFINES
    address_space

SEES
    constants,
    constants_statuscodes_bs,
    service_write_decode_bs,
    data_value_pointer_bs

DEFINITIONS
    d_variables ==
        a_NodeId,
        a_NodeClass,
        a_BrowseName,
        a_DisplayName,
        a_Value,
        a_Value_StatusCode,
        a_Value_SourceTimestamp,
        a_IsAbstract,
        a_EventNotifier,
        a_DataType,
        a_ValueRank,
        a_ArrayDimensions,
        a_AccessLevel,
        a_UserAccessLevel,
        a_Historizing,
        a_Executable,
        a_UserExecutable,
        a_ContainsNoLoops,
        a_Symmetric,

        Type_IsTransitiveSubtype,
        Type_IsReferenceTypeIdValid,
        
        Object_HasMethod,
        Variant_LocalDataType,
        Variant_ValueRank,

        Ref_HasTypeDefinition,

        Reference_SourceNode,
        Reference_ReferenceType,
        Reference_TargetNode,
        Reference_IsForward,

        Node_Reference,
        Node_RefIndexEnd,
        RefIndex_Reference,

        IsRangeDefined,
        IsValid_VariantIndex,
        HasRange_VariantIndex,

        /* Local treament */
        local_service_treatment,

        /* Service Write */
        ResponseWrite_allocated,
        ResponseWrite_StatusCode,

        /* User authorization */
        s_AuthorizedOperations
    ;

    d_inv ==
        /* Attributes */
        a_NodeId      : t_Node >-> t_NodeId        &
        a_NodeClass   : t_Node --> t_NodeClass     &
        a_BrowseName  : t_Node --> t_QualifiedName &
        a_DisplayName : t_Node --> t_LocalizedText &

        a_Value : t_Node +-> t_Variant &
        dom(a_NodeClass|>{e_ncl_Variable}) <: dom(a_Value) &
        dom(a_Value) <: dom(a_NodeClass|>{e_ncl_Variable, e_ncl_VariableType}) &

        /* + Value attribute meta-data */
        a_Value_StatusCode : a_NodeClass~[{e_ncl_Variable}] --> t_RawStatusCode &
        a_Value_SourceTimestamp : a_NodeClass~[{e_ncl_Variable}] --> t_Timestamp &

        a_IsAbstract : a_NodeClass~[{e_ncl_ObjectType, e_ncl_VariableType, e_ncl_ReferenceType, e_ncl_DataType}] --> BOOL &

        a_EventNotifier : a_NodeClass~[{e_ncl_Object, e_ncl_View}] --> t_Byte &

        a_DataType : a_NodeClass~[{e_ncl_Variable, e_ncl_VariableType}] --> t_NodeId &

        a_ValueRank : a_NodeClass~[{e_ncl_Variable, e_ncl_VariableType}] --> t_ValueRank &
        
        a_ArrayDimensions : a_NodeClass~[{e_ncl_Variable, e_ncl_VariableType}] --> t_ArrayDimensions &

        a_AccessLevel : a_NodeClass~[{e_ncl_Variable}] --> t_access_level &

        a_UserAccessLevel : a_NodeClass~[{e_ncl_Variable}] --> t_access_level &

        a_Historizing : a_NodeClass~[{e_ncl_Variable}] --> BOOL &

        a_Executable : a_NodeClass~[{e_ncl_Method}] --> BOOL &

        a_UserExecutable : a_NodeClass~[{e_ncl_Method}] --> BOOL &

        a_ContainsNoLoops : a_NodeClass~[{e_ncl_View}] --> BOOL &

        a_Symmetric : a_NodeClass~[{e_ncl_ReferenceType}] --> BOOL &

        /* Types */
        Type_IsTransitiveSubtype : t_NodeId <-> t_NodeId &
        Type_IsReferenceTypeIdValid <: t_NodeId &
        
        /* Objects */
        Object_HasMethod : t_NodeId <-> t_NodeId &

		Variant_LocalDataType : t_Variant --> t_NodeId &
        Variant_ValueRank : t_Variant --> t_ConcreteValueRank &
		
        /* References */
        Ref_HasTypeDefinition : t_Node +-> t_ExpandedNodeId &
        dom(Ref_HasTypeDefinition) = dom(a_NodeClass |> {e_ncl_Variable, e_ncl_Object}) &

        Reference_SourceNode    : t_Reference --> t_NodeId         &
        Reference_ReferenceType : t_Reference --> t_NodeId         &
        Reference_TargetNode    : t_Reference --> t_ExpandedNodeId &
        Reference_IsForward     : t_Reference --> BOOL             &

        Node_Reference          : t_Node     <-> t_Reference &
        Node_RefIndexEnd        : t_Node     --> t_RefIndex  &
        RefIndex_Reference      : t_Node * t_RefIndex --> t_Reference &

        IsRangeDefined : t_IndexRange_i --> BOOL &
        IsValid_VariantIndex : t_Variant * t_IndexRange_i --> BOOL &
        HasRange_VariantIndex  : t_Variant * t_IndexRange_i --> BOOL &

        /* Local treament */
        local_service_treatment : NAT &

        /* Service Write */
        ResponseWrite_allocated : BOOL &
        ResponseWrite_StatusCode : t_WriteValue +-> t_StatusCode &

        /* User authorization */
        s_AuthorizedOperations <: t_operation_type * t_NodeId * t_AttributeId * t_user
    ;

    d_init ==
        d_inv &
        local_service_treatment = 0 &
        ResponseWrite_allocated = FALSE &
        ResponseWrite_StatusCode = {}
    ;

    /* Relation associating to a node a value which is requested to be written
       in the address space */
    d_a_DataValue_requested ==
        (a_NodeClass~[{e_ncl_Variable}]           <| a_NodeId ;
                                                     WriteValue_NodeId~ ;
         (cast_t_WriteValue[1..nb_WriteValue] /\
          WriteValue_AttributeId~[{e_aid_Value}]) <| WriteValue_DataValue)

ABSTRACT_VARIABLES
    d_variables

INITIALISATION
    d_variables :( d_init )

OPERATIONS

    /* --------------------------------------------------------------------- */
    /* Service Write */

    StatusCode_service <-- treat_write_request_WriteValues (p_user, p_locales) =
    PRE
        p_user : t_user_i &
        p_user : t_user   &
        p_locales : t_LocaleIds_i &
        p_locales : t_LocaleIds
    THEN
        StatusCode_service := e_sc_ok ||
        a_Value,
        a_Value_StatusCode,
        a_Value_SourceTimestamp,
        ResponseWrite_StatusCode
        :(  ResponseWrite_StatusCode : t_WriteValue +-> t_StatusCode &

            /* For a given node, the write request message may provide different values to be written
               as the value attribute of this node. In this case, the written value is chosen among
               possible values. */

            /* At most one value should be written for each node */
            a_Value : t_Node +-> t_Variant &

            /* The value requested to be written should be contained in the requested values */
            a_Value <: a_Value$0 <+ (d_a_DataValue_requested;DataValue_Variant) &

            /* Same for meta-attributes */
            a_Value_StatusCode <: a_Value_StatusCode$0 <+ (d_a_DataValue_requested;DataValue_StatusCode) &
            a_Value_SourceTimestamp <: a_Value_SourceTimestamp$0 <+ (d_a_DataValue_requested;DataValue_SourceTimestamp) &

            ResponseWrite_StatusCode[cast_t_WriteValue[1..nb_WriteValue]]
            <: {e_sc_ok, e_sc_bad_out_of_memory,
                e_sc_bad_attribute_id_invalid, e_sc_bad_index_range_invalid,
                e_sc_bad_node_id_unknown, e_sc_bad_not_writable, e_sc_bad_type_mismatch,
                e_sc_bad_user_access_denied, e_sc_bad_index_range_no_data}
        )
    END
    ;

    bret <-- alloc_write_request_responses(nb_req) =
    BEGIN
        IF nb_req <= k_n_WriteResponse_max THEN
            bret,
            ResponseWrite_StatusCode,
            ResponseWrite_allocated
            :(
                bret : BOOL &
                ResponseWrite_allocated  : BOOL                          &
                ResponseWrite_StatusCode : t_WriteValue +-> t_StatusCode &
                (ResponseWrite_allocated = TRUE
                 =>
                 ResponseWrite_StatusCode = cast_t_WriteValue[1..nb_req] * {e_sc_bad_internal_error}) &
                (ResponseWrite_allocated = FALSE
                 =>
                 ResponseWrite_StatusCode = {}) &
                bret = ResponseWrite_allocated
            )
        ELSE
            ResponseWrite_allocated := FALSE ||
            bret := FALSE
        END
    END

END
