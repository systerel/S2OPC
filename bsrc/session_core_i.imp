/*
 * Licensed to Systerel under one or more contributor license
 * agreements. See the NOTICE file distributed with this work
 * for additional information regarding copyright ownership.
 * Systerel licenses this file to you under the Apache
 * License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License. You may obtain
 * a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

IMPLEMENTATION
    session_core_i
REFINES
    session_core

DEFINITIONS
    d_active_sessions   == ( sessions_iterated /\ a_channel~[{p_lost_channel}] /\ a_state~[{e_session_userActivated}]);
    d_inactive_sessions == ((sessions_iterated /\ a_channel~[{p_lost_channel}]) - a_state~[{e_session_userActivated}])

SEES
    constants,
    constants_statuscodes_bs,
    message_in_bs,
    message_out_bs,
    channel_mgr,
    request_handle_bs

IMPORTS
    session_core_1,
    session_core_it

PROMOTES
    server_get_session_from_token,
    client_get_token_from_session,
    getall_session_channel,
    get_session_channel,
    set_session_to_create,
    set_session_app_context,
    getall_to_create,
    getall_orphaned,
    reset_session_to_create,
    is_valid_session,
    drop_user_server,
    client_gen_create_session_internal_event,
    client_gen_activate_orphaned_session_internal_event,
    client_gen_activate_user_session_internal_event,
    server_session_timeout_start_timer,
    server_session_timeout_evaluation,
    server_session_timeout_msg_received,
    set_session_user_client,
    get_session_user_client,
    get_session_user_server

LOCAL_OPERATIONS

    res_activated <-- server_internal_activate_req_and_resp (channel, session, transitoryState, p_user, activate_req_msg, activate_resp_msg) =
    PRE
        channel       : t_channel_i &
        channel       : s_channel_connected &
        session       : t_session_i &
        session       : s_session &
        transitoryState : t_sessionState & /* used to differentiate transitions externally */
        p_user          : t_user_i &
        p_user          : t_user &
        dom(a_state) = s_session &
        a_state(session) : {e_session_created, e_session_userActivated, e_session_scOrphaned} &
        activate_req_msg : t_msg_i &
        activate_req_msg = c_msg_in &
        c_msg_in : t_msg &
        a_msg_in_type = e_msg_session_activate_req &
        activate_resp_msg : t_msg_i &
        activate_resp_msg = c_msg_out &
        c_msg_out : t_msg &
        a_msg_out_type = e_msg_session_activate_resp
    THEN
        CHOICE
            a_state(session) := e_session_userActivated ||
            a_channel(session) := channel ||
            a_user_server(session) := p_user ||
            res_activated := TRUE ||
            a_NonceServer
            :(
                a_NonceServer : t_session +-> t_Nonce &
                {session} <<| a_NonceServer = {session} <<| a_NonceServer$0 &
                session : dom(a_NonceServer)
            )
        OR
            res_activated := FALSE
        END
    END
    ;

    l_client_secure_channel_lost_session_sm(p_dom, p_channel, p_lost_channel, p_session, p_channel_config_idx) =
    PRE
        p_dom                : BOOL                   &
        p_channel            : t_channel_i            &
        p_session            : t_session_i            &
        p_lost_channel       : t_channel_i            &
        p_session            : t_session_i            &
        p_channel_config_idx : t_channel_config_idx_i &
        p_channel_config_idx : t_channel_config_idx   &
        (   p_dom = TRUE
            =>
            p_session : dom(a_channel) &
            p_session : dom(a_state) &
            p_channel = a_channel(p_session))
    THEN
        s_session,
        a_state,
        a_channel,
        a_client_to_create,
        a_client_app_context,
        a_client_orphaned,
        a_user_server,
        a_user_client,
        a_server_token,
        a_client_token,
        a_NonceServer,
        a_NonceClient
        :(
            s_session                   <: t_session                             &
            a_state                      : t_session +-> t_sessionState          &
            a_channel                    : t_session +-> t_channel               &
            a_client_to_create           : t_session +-> t_channel_config_idx_i  &
            a_client_app_context         : t_session +-> t_application_context_i &
            a_client_orphaned            : t_session +-> t_channel_config_idx_i  &
            a_client_token               : t_session +-> t_session_token         &
            a_server_token               : t_session_token +-> t_session         &
            a_user_server                : t_session +-> t_user                  &
            a_user_client                : t_session +-> t_user_token            &
            a_NonceServer                : t_session +-> t_Nonce                 &
            a_NonceClient                : t_session +-> t_Nonce                 &

            (   p_dom     = TRUE &
                p_channel = p_lost_channel
                =>
                (   a_state$0(p_session) = e_session_userActivated
                    =>
                    a_channel                    = {p_session} <<| a_channel$0 &
                    a_state                      = a_state$0 <+ {p_session |-> e_session_scOrphaned} &
                    s_session                    = s_session$0 &
                    a_client_to_create           = a_client_to_create$0 &
                    a_client_app_context         = a_client_app_context$0 &
                    a_user_server                = a_user_server$0 &
                    a_user_client                = a_user_client$0 &
                    a_server_token               = a_server_token$0 &
                    a_client_token               = a_client_token$0 &
                    a_NonceServer                = a_NonceServer$0 &
                    a_NonceClient                = a_NonceClient$0 &
                    a_client_orphaned = a_client_orphaned$0 <+ {p_session |-> p_channel_config_idx}
                ) &
                (   a_state$0(p_session) /= e_session_userActivated
                    =>
                    s_session                    = s_session$0 - {p_session} &
                    a_state                      = {p_session} <<| a_state$0 &
                    a_channel                    = {p_session} <<| a_channel$0 &
                    a_client_to_create           = {p_session} <<| a_client_to_create$0 &
                    a_client_app_context         = {p_session} <<| a_client_app_context$0 &
                    a_client_orphaned            = {p_session} <<| a_client_orphaned$0 &
                    a_user_server                = {p_session} <<| a_user_server$0 &
                    a_user_client                = {p_session} <<| a_user_client$0 &
                    a_server_token               = a_server_token$0 |>> {p_session} &
                    a_client_token               = {p_session} <<| a_client_token$0 &
                    a_NonceServer                = {p_session} <<| a_NonceServer$0 &
                    a_NonceClient                = {p_session} <<| a_NonceClient$0
                )
            ) &
            (   not(p_dom     = TRUE &
                    p_channel = p_lost_channel)
                =>
                s_session                    = s_session$0 &
                a_state                      = a_state$0 &
                a_channel                    = a_channel$0 &
                a_client_to_create           = a_client_to_create$0 &
                a_client_app_context         = a_client_app_context$0 &
                a_client_orphaned            = a_client_orphaned$0 &
                a_user_server                = a_user_server$0 &
                a_user_client                = a_user_client$0 &
                a_server_token               = a_server_token$0 &
                a_client_token               = a_client_token$0 &
                a_NonceServer                = a_NonceServer$0 &
                a_NonceClient                = a_NonceClient$0
            )
        )
    END
    ;

    l_server_secure_channel_lost_session_sm(p_dom, p_channel, p_lost_channel, p_session) =
    PRE
        p_dom          : BOOL        &
        p_channel      : t_channel_i &
        p_session      : t_session_i &
        p_lost_channel : t_channel_i &
        p_session      : t_session_i &
        (   p_dom = TRUE
            =>
            p_session : dom(a_channel) &
            p_session : dom(a_state) &
            p_channel = a_channel(p_session))
    THEN
        s_session,
        a_state,
        a_channel,
        a_client_to_create,
        a_client_app_context,
        a_client_orphaned,
        a_user_server,
        a_user_client,
        a_server_token,
        a_client_token,
        a_NonceServer,
        a_NonceClient
        :(
            s_session                   <: t_session                             &
            a_state                      : t_session +-> t_sessionState          &
            a_channel                    : t_session +-> t_channel               &
            a_client_to_create           : t_session +-> t_channel_config_idx_i  &
            a_client_app_context         : t_session +-> t_application_context_i &
            a_client_orphaned            : t_session +-> t_channel_config_idx_i  &
            a_client_token               : t_session +-> t_session_token         &
            a_server_token               : t_session_token +-> t_session         &
            a_user_server                : t_session +-> t_user                  &
            a_user_client                : t_session +-> t_user_token            &
            a_NonceServer                : t_session +-> t_Nonce                 &
            a_NonceClient                : t_session +-> t_Nonce                 &

            (   p_dom     = TRUE &
                p_channel = p_lost_channel
                =>
                (   a_state$0(p_session) = e_session_userActivated
                    =>
                    a_channel                    = {p_session} <<| a_channel$0 &
                    a_state                      = a_state$0 <+ {p_session |-> e_session_scOrphaned} &
                    s_session                    = s_session$0 &
                    a_client_to_create           = a_client_to_create$0 &
                    a_client_app_context         = a_client_app_context$0 &
                    a_user_server                = a_user_server$0 &
                    a_user_client                = a_user_client$0 &
                    a_server_token               = a_server_token$0 &
                    a_client_token               = a_client_token$0 &
                    a_NonceServer                = a_NonceServer$0 &
                    a_NonceClient                = a_NonceClient$0 &
                    a_client_orphaned            = a_client_orphaned$0
                ) &
                (   a_state$0(p_session) /= e_session_userActivated
                    =>
                    s_session                    = s_session$0 - {p_session} &
                    a_state                      = {p_session} <<| a_state$0 &
                    a_channel                    = {p_session} <<| a_channel$0 &
                    a_client_to_create           = {p_session} <<| a_client_to_create$0 &
                    a_client_app_context         = {p_session} <<| a_client_app_context$0 &
                    a_client_orphaned            = {p_session} <<| a_client_orphaned$0 &
                    a_user_server                = {p_session} <<| a_user_server$0 &
                    a_user_client                = {p_session} <<| a_user_client$0 &
                    a_server_token               = a_server_token$0 |>> {p_session} &
                    a_client_token               = {p_session} <<| a_client_token$0 &
                    a_NonceServer                = {p_session} <<| a_NonceServer$0 &
                    a_NonceClient                = {p_session} <<| a_NonceClient$0
                )
            ) &
            (   not(p_dom     = TRUE &
                    p_channel = p_lost_channel)
                =>
                s_session                    = s_session$0 &
                a_state                      = a_state$0 &
                a_channel                    = a_channel$0 &
                a_client_to_create           = a_client_to_create$0 &
                a_client_app_context         = a_client_app_context$0 &
                a_client_orphaned            = a_client_orphaned$0 &
                a_user_server                = a_user_server$0 &
                a_user_client                = a_user_client$0 &
                a_server_token               = a_server_token$0 &
                a_client_token               = a_client_token$0 &
                a_NonceServer                = a_NonceServer$0 &
                a_NonceClient                = a_NonceClient$0
            )
        )
    END


OPERATIONS

    res_activated <-- server_internal_activate_req_and_resp (channel, session, transitoryState, p_user, activate_req_msg, activate_resp_msg) =
    VAR
        l_channel_config_idx,
        l_secpol,
        l_nonce
    IN
        l_channel_config_idx <-- get_channel_info (channel);
        l_secpol <-- get_SecurityPolicy(channel);
        IF l_secpol /= e_secpol_None THEN
            res_activated <-- server_activate_session_check_crypto(session, channel, l_channel_config_idx, activate_req_msg);
            IF res_activated = TRUE THEN
                l_nonce <-- get_NonceServer(session);
                write_activate_session_resp_msg_crypto(activate_resp_msg, l_nonce)
            END
        ELSE
            res_activated := TRUE
        END;
        IF res_activated = TRUE THEN
            set_session_state (session, transitoryState, FALSE);
            drop_user_server (session); // In case a previous user was set, drop it
            set_session_user_server (session, p_user);
            set_session_channel (session, channel);
            set_session_state (session, e_session_userActivated, FALSE)
        END
    END
    ;

    /* END OF LOCAL OPERATIONS */
    nsession <-- client_init_session_sm =
    BEGIN
        nsession <-- init_new_session (TRUE)
    END;

    valid <-- client_create_session_req_sm (session, channel, create_req_msg) =
    VAR
        l_channel_config_idx,
        l_nonce_needed,
        l_nonce
    IN
        create_session (session, channel, e_session_creating, TRUE);
        l_channel_config_idx <-- get_channel_info (channel);
        valid, l_nonce_needed <-- client_create_session_req_do_crypto(session, channel, l_channel_config_idx);
        IF valid = TRUE THEN
            write_create_session_req_msg_sessionTimeout (create_req_msg);
            write_create_session_req_msg_endpointUrl (create_req_msg, l_channel_config_idx);
            IF l_nonce_needed = TRUE THEN
                l_nonce <-- get_NonceClient(session);
                valid <-- write_create_session_req_msg_crypto(create_req_msg, l_channel_config_idx, l_nonce)
            END
            /* TODO: Fill remaining parameters: sessionName, maxResponseSize (keep 0 => unused ?)*/
            /*write_create_session_req_msg_fill(create_req_msg, l_channel_config_idx) */
        END
    END
    ;

    nsession, service_ret <-- server_create_session_req_and_resp_sm (channel, create_req_msg, create_resp_msg) =
    VAR
        l_nsession,
        l_valid_session,
        l_nsession_token,
        l_valid_session_token,
        l_endpoint_config_idx,
        /*l_valid_req,*/
        l_config_idx,
        l_crypto_status,
        l_signature,
        l_nonce,
        l_secpol,
        l_bret
    IN
        service_ret := e_sc_bad_out_of_memory;
        l_nsession <-- init_new_session (FALSE);
        l_valid_session <-- is_valid_session (l_nsession);
        IF l_valid_session = TRUE THEN
            create_session(l_nsession, channel, e_session_created, FALSE);
            /* TODO: check request parameters: client certificate */
            /*l_valid_req <-- server_create_session_valid_req(create_req_msg) */
            l_nsession_token <-- server_get_fresh_session_token (l_nsession);
            l_valid_session_token <-- server_is_valid_session_token (l_nsession_token);
            IF l_valid_session_token = TRUE /*& l_valid_req = TRUE */
            THEN
                l_endpoint_config_idx <-- server_get_endpoint_config (channel);
                write_create_session_msg_session_token (create_resp_msg, l_nsession_token);
                /* Set revised timeout to : MIN <= requested timeout <= MAX */
                write_create_session_msg_session_revised_timeout (create_req_msg, create_resp_msg);
                service_ret <-- write_create_session_msg_server_endpoints (create_req_msg, create_resp_msg, l_endpoint_config_idx);
                /* TODO: populate msg with remaining parameters: MaxRequestSize, RevisedSessionTimeout, CertServer, ... */
                IF service_ret = e_sc_ok THEN
                    l_secpol <-- get_SecurityPolicy(channel);
                    IF l_secpol /= e_secpol_None THEN
                        /* Do the cryptographic challenge which may fail because of mallocs */
                        l_config_idx <-- get_channel_info(channel); /* MAYBE this is denoted client side only... */
                        l_crypto_status, l_signature <-- server_create_session_req_do_crypto(l_nsession, create_req_msg, l_endpoint_config_idx, l_config_idx);
                        IF  l_crypto_status = e_sc_ok THEN
                            l_nonce <-- get_NonceServer(l_nsession);
                            l_bret <-- write_create_session_resp_msg_crypto(create_resp_msg, l_config_idx, l_nonce, l_signature);
                            clear_Signature(l_nsession, l_signature);
                            IF l_bret = FALSE THEN
                                service_ret := e_sc_bad_unexpected_error
                            END
                        ELSE
                            service_ret := l_crypto_status
                        END
                    END
                END
            ELSE
                service_ret := e_sc_bad_too_many_sessions;
                set_session_state_closed (l_nsession, e_sc_bad_session_id_invalid, FALSE);
                l_nsession := c_session_indet
            END
        ELSE
            service_ret := e_sc_bad_too_many_sessions
        END;
        nsession := l_nsession
    END
    ;

    bret <-- client_create_session_resp_sm (channel, session, session_token, create_resp_msg) =
    VAR
        l_nonce,
        l_valid,
        l_channel_config_idx,
        l_secpol
    IN
        /* TODO: check Request:handle == Response:handle ; Response:Cert_server == Cert_server */
        /* TODO? check Response:serverEndpoints */
        /* Check server signature */
        bret := TRUE;
        l_secpol <-- get_SecurityPolicy(channel);
        IF l_secpol /= e_secpol_None THEN
            /* Note: receiving a request implies that we sent it, and Nonce, if needed, was valid, hence get_NonceClient cannot fail. */
            /* TODO: at least validate nonce value. In this case it could also be better to have it as parameter of check_crypto ! */
            l_nonce <-- get_NonceClient(session);
            l_channel_config_idx <-- get_channel_info (channel);
            l_valid <-- client_create_session_check_crypto(session, l_channel_config_idx, create_resp_msg);
            IF l_valid = TRUE THEN
                // channel: only for the precondition
                set_session_state (session, e_session_created, TRUE);
                client_set_session_token (session, session_token);
                drop_NonceClient(session)
            ELSE
                bret := FALSE
            END
        ELSE
            set_session_state (session, e_session_created, TRUE);
            client_set_session_token (session, session_token)
        END
    END
    ;

    ret, channel, session_token <-- client_user_activate_session_req_sm (session, p_user_token, activate_req_msg) =
    VAR
        l_is_connected_channel,
        l_channel_config_idx,
        l_secpol,
        l_valid_crypto,
        l_signature,
        l_server_nonce,
        l_bret
    IN
        channel <-- get_session_channel (session);
        l_is_connected_channel <-- is_connected_channel(channel);
        IF l_is_connected_channel = TRUE THEN
            session_token <-- client_get_token_from_session (session);
            l_channel_config_idx <-- get_channel_info (channel);
            /* TODO: check validity of p_user_token */
            /* TODO: populate msg with parameters */
            write_activate_msg_user(activate_req_msg, p_user_token);
            l_secpol <-- get_SecurityPolicy(channel);
            IF l_secpol /= e_secpol_None THEN
                l_server_nonce <-- get_NonceServer(session);
                // TODO: validate server nonce ? Or guaranteed by session state ?
                l_valid_crypto, l_signature <-- client_activate_session_req_do_crypto(session, l_channel_config_idx, l_server_nonce);
                IF l_valid_crypto = TRUE THEN
                    l_bret <-- write_activate_session_req_msg_crypto(activate_req_msg, l_signature);
                    clear_Signature(session, l_signature);
                    IF l_bret = TRUE THEN
                        ret := e_sc_ok
                    ELSE
                        ret := e_sc_bad_unexpected_error
                    END
                ELSE
                    /* TODO: add bad security check failed error and use it instead */
                    ret := e_sc_bad_unexpected_error
                END
            ELSE
                ret := e_sc_ok
            END
        ELSE
            channel := c_channel_indet;
            session_token := c_session_token_indet;
            ret := e_sc_bad_unexpected_error
        END;
        IF ret = e_sc_ok THEN
            set_session_state(session, e_session_userActivating, TRUE)
        END
    END
    ;

    session_token <-- client_sc_activate_session_req_sm (session, channel, activate_req_msg) =
    VAR
        l_user_token
    IN
        session_token <-- client_get_token_from_session (session);
        l_user_token <-- get_session_user_client (session);
        /* TODO: populate msg with parameters  */
        write_activate_msg_user(activate_req_msg, l_user_token);
        set_session_channel(session, channel);
        set_session_state(session, e_session_scActivating, TRUE)
    END
    ;

    ret <-- server_activate_session_req_and_resp_sm (channel, session, user, activate_req_msg, activate_resp_msg) =
    VAR
        l_channel,
        l_state,
        l_user,
        l_is_same_user,
        l_valid,
        l_ret
    IN
        l_channel <-- get_session_channel (session);
        l_state <-- get_session_state (session);
        IF l_state = e_session_created
        THEN
            /* Channel must be the same and user must be set */
            IF l_channel = channel
            THEN
                l_valid <-- server_internal_activate_req_and_resp (channel,
                                                                   session,
                                                                   e_session_creating,
                                                                   user,
                                                                   activate_req_msg,
                                                                   activate_resp_msg);
                IF l_valid = TRUE THEN
                    l_ret := e_sc_ok
                ELSE
                    /* TODO: return detailed error from transition */
                    l_ret := e_sc_bad_unexpected_error
                END
            ELSE
                l_ret := e_sc_bad_invalid_argument
            END
        ELSIF l_state = e_session_userActivated
        THEN
            l_user <-- get_session_user_server (session);
            l_is_same_user <-- is_same_user_server (l_user, user);
            /* User changed XOR channel changed */
            IF l_channel = channel &
                l_is_same_user = FALSE
            THEN
                l_valid <-- server_internal_activate_req_and_resp (channel,
                                                                   session,
                                                                   e_session_userActivating,
                                                                   user,
                                                                   activate_req_msg,
                                                                   activate_resp_msg);
                IF l_valid = TRUE THEN
                    l_ret := e_sc_ok
                ELSE
                    /* TODO: return detailed error from transition */
                    l_ret := e_sc_bad_unexpected_error
                END
            ELSIF l_channel /= channel &
                l_is_same_user = TRUE
            THEN
                l_valid <-- server_internal_activate_req_and_resp (channel,
                                                                   session,
                                                                   e_session_scActivating,
                                                                   user,
                                                                   activate_req_msg,
                                                                   activate_resp_msg);
                IF l_valid = TRUE THEN
                    l_ret := e_sc_ok
                ELSE
                    /* TODO: return detailed error from transition */
                    l_ret := e_sc_bad_unexpected_error
                END
            ELSE
                l_ret := e_sc_bad_invalid_state
            END
        ELSIF l_state = e_session_scOrphaned
        THEN
            l_user <-- get_session_user_server (session);
            l_is_same_user <-- is_same_user_server (l_user, user);
            /* Channel changed */
            IF l_channel /= channel &
                l_is_same_user = TRUE
            THEN
                l_valid <-- server_internal_activate_req_and_resp (channel,
                                                                   session,
                                                                   e_session_scActivating,
                                                                   user,
                                                                   activate_req_msg,
                                                                   activate_resp_msg);
                IF l_valid = TRUE THEN
                    l_ret := e_sc_ok
                ELSE
                    /* TODO: return detailed error from transition */
                    l_ret := e_sc_bad_unexpected_error
                END
            ELSE
                l_ret := e_sc_bad_invalid_state
            END
        ELSE
            l_ret := e_sc_bad_invalid_state
        END;
        ret := l_ret
    END
    ;

    bret <-- client_activate_session_resp_sm (channel , session, activate_resp_msg) =
    VAR
        l_channel_config_idx,
        l_valid_response
    IN
        /* PRE checked channel was session channel*/
        l_channel_config_idx <-- get_channel_info (channel);
        /* TODO: check response parameters + nonce param if applicable */
        l_valid_response <-- client_activate_session_resp_check(session, l_channel_config_idx,  activate_resp_msg);
        IF l_valid_response = TRUE
        THEN
            set_session_state (session, e_session_userActivated, TRUE);
            bret := TRUE
        ELSE
            bret := FALSE
        END
    END
    ;

    l_client_secure_channel_lost_session_sm(p_dom, p_channel, p_lost_channel, p_session, p_channel_config_idx) =
    VAR
        l_state
    IN
        IF  p_dom = TRUE &
            p_channel = p_lost_channel
        THEN
            l_state <-- get_session_state (p_session);
            IF  l_state = e_session_userActivated
            THEN
                /* Active sessions set to orphaned and channel config idx associated */
                set_session_orphaned (p_session, p_channel_config_idx);
                set_session_state (p_session, e_session_scOrphaned, TRUE)
            ELSE
                /* Inactive sessions or all orphaned sessions (if no new channel can be associated) */
                set_session_state_closed (p_session, e_sc_bad_secure_channel_closed , TRUE)
            END
        END
    END
    ;

    client_secure_channel_lost_session_sm (p_lost_channel, p_channel_config_idx) =
    VAR
        l_continue,
        l_session,
        l_dom,
        l_channel
    IN
        l_continue <-- init_iter_session;
        IF  l_continue = TRUE THEN  /* Only for proving */
            WHILE l_continue = TRUE DO
                l_continue, l_session <-- continue_iter_session;
                l_dom, l_channel <-- getall_session_channel(l_session);
                l_client_secure_channel_lost_session_sm(l_dom, l_channel, p_lost_channel, l_session, p_channel_config_idx)
            INVARIANT
                sessions_to_iterate <: t_session &
                sessions_iterated   <: t_session &
                sessions_to_iterate /\ sessions_iterated = {} &
                sessions_to_iterate \/ sessions_iterated = t_session &
                l_continue = bool(sessions_to_iterate /= {}) &
                dom(a_channel) <: dom(a_state) &
                s_session                    = s_session$0 - d_inactive_sessions                                                         &
                a_state                      = d_inactive_sessions <<| a_state$0 <+ d_active_sessions * {e_session_scOrphaned}           &
                a_channel                    = d_inactive_sessions <<| a_channel$0 |>> {p_lost_channel}                                  &
                a_client_to_create           = d_inactive_sessions <<| a_client_to_create$0                                              &
                a_client_app_context         = d_inactive_sessions <<| a_client_app_context$0                                            &
                a_client_orphaned            = d_inactive_sessions <<| a_client_orphaned$0 <+ d_active_sessions * {p_channel_config_idx} &
                a_user_server                = d_inactive_sessions <<| a_user_server$0                                                   &
                a_user_client                = d_inactive_sessions <<| a_user_client$0                                                   &
                a_server_token               = a_server_token$0 |>> d_inactive_sessions                                                  &
                a_client_token               = d_inactive_sessions <<| a_client_token$0                                                  &
                a_NonceServer                = d_inactive_sessions <<| a_NonceServer$0                                                   &
                a_NonceClient                = d_inactive_sessions <<| a_NonceClient$0
            VARIANT
                card(sessions_to_iterate)
            END
        END
    END
    ;


    l_server_secure_channel_lost_session_sm(p_dom, p_channel, p_lost_channel, p_session) =
    VAR
        l_state
    IN
        IF  p_dom = TRUE &
            p_channel = p_lost_channel
        THEN
            l_state <-- get_session_state (p_session);
            IF l_state = e_session_userActivated
            THEN
                set_session_orphaned (p_session, c_channel_config_idx_indet);
                set_session_state (p_session, e_session_scOrphaned, FALSE)
            ELSE
                set_session_state_closed (p_session, e_sc_bad_secure_channel_closed , FALSE)
            END
        END
    END
    ;

    server_secure_channel_lost_session_sm (p_lost_channel) =
    VAR
        l_continue,
        l_session,
        l_dom,
        l_channel
    IN
        l_continue <-- init_iter_session;
        IF  l_continue = TRUE THEN  /* Only for proving */
            WHILE l_continue = TRUE DO
                l_continue, l_session <-- continue_iter_session;
                l_dom, l_channel <-- getall_session_channel(l_session);
                l_server_secure_channel_lost_session_sm(l_dom, l_channel, p_lost_channel, l_session)
            INVARIANT
                sessions_to_iterate <: t_session &
                sessions_iterated   <: t_session &
                sessions_to_iterate /\ sessions_iterated = {} &
                sessions_to_iterate \/ sessions_iterated = t_session&
                l_continue = bool(sessions_to_iterate /= {}) &
                dom(a_channel) <: dom(a_state) &
                s_session                    = s_session$0 - d_inactive_sessions                                               &
                a_state                      = d_inactive_sessions <<| a_state$0 <+ d_active_sessions * {e_session_scOrphaned} &
                a_channel                    = d_inactive_sessions <<| a_channel$0 |>> {p_lost_channel}                        &
                a_client_to_create           = d_inactive_sessions <<| a_client_to_create$0                                    &
                a_client_app_context         = d_inactive_sessions <<| a_client_app_context$0                                  &
                a_client_orphaned            = d_inactive_sessions <<| a_client_orphaned$0                                     &
                a_user_server                = d_inactive_sessions <<| a_user_server$0                                         &
                a_user_client                = d_inactive_sessions <<| a_user_client$0                                         &
                a_server_token               = a_server_token$0 |>> d_inactive_sessions                                        &
                a_client_token               = d_inactive_sessions <<| a_client_token$0                                        &
                a_NonceServer                = d_inactive_sessions <<| a_NonceServer$0                                         &
                a_NonceClient                = d_inactive_sessions <<| a_NonceClient$0
            VARIANT
                card(sessions_to_iterate)
            END
        END
    END
    ;

    ret, channel, session_token <-- client_close_session_req_sm (session, close_req_msg) =
    VAR
        l_is_connected_channel
    IN
        /* PRE check request msg type expected */
        /* TODO: fulfill delete subscriptions flag (to be provided as param ?) */
        client_close_session_req_msg(close_req_msg); /* Avoid unused variable*/

        channel <-- get_session_channel (session);
        l_is_connected_channel <-- is_connected_channel(channel);
        IF l_is_connected_channel = TRUE
        THEN
            session_token <-- client_get_token_from_session (session);
            /* TODO: populate msg with parameters */
            set_session_state (session, e_session_closing, TRUE);
            ret := e_sc_ok
        ELSE
            channel := c_channel_indet;
            session_token := c_session_token_indet;
            ret := e_sc_bad_unexpected_error
        END

    END
    ;

    ret <-- server_close_session_req_and_resp_sm (channel, session, close_req_msg, close_resp_msg) =
    BEGIN
        /* PRE guarantee channel is connected */
        channel_do_nothing (channel); /* Avoid unused variable */
        /* TODO: actions on pending requests or current subscriptions to manage (see request) */
        server_close_session_check_req (close_req_msg, close_resp_msg);
        /* No need to store the handle since we will send response in same step */
        set_session_state_closed (session, e_sc_ok , FALSE);
        ret := e_sc_ok
    END
    ;

    client_close_session_resp_sm (channel, session, close_resp_msg) =
    BEGIN
        /* PRE guarantee channel is connected */
        channel_do_nothing (channel); /* Avoid unused variable */
        client_close_session_resp_msg(close_resp_msg); /* Avoid unused variable*/
        /* In any case we close the session: correct channel or not  */
        set_session_state_closed (session, e_sc_ok, TRUE)
    END
    ;

    client_close_session_sm (session, sc_reason) =
    BEGIN
        set_session_state_closed (session, sc_reason, TRUE)
    END
    ;

    server_close_session_sm (session, sc_reason) =
    BEGIN
        set_session_state_closed (session, sc_reason, FALSE)
    END
    ;

    ret <-- is_session_valid_for_service (channel, session) =
    BEGIN
        // only for PRE compliance
        channel_do_nothing (channel); /* Avoid unused variable */
        session_do_nothing (session); /* Avoid unused variable */
        ret := TRUE
    END
    ;

    state <-- get_session_state_or_closed (session) =
    VAR
        l_valid_session
    IN
        l_valid_session <-- is_valid_session (session);
        IF l_valid_session = TRUE
        THEN
            state <-- get_session_state (session)
        ELSE
            state := e_session_closed
        END
    END

END
