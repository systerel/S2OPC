/*
 * Licensed to Systerel under one or more contributor license
 * agreements. See the NOTICE file distributed with this work
 * for additional information regarding copyright ownership.
 * Systerel licenses this file to you under the Apache
 * License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License. You may obtain
 * a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

IMPLEMENTATION
    user_authentication_i

REFINES
    user_authentication

SEES
    constants,
    constants_statuscodes_bs

IMPORTS
    user_authentication_bs

PROMOTES
    deallocate_user,
    get_local_user

LOCAL_OPERATIONS

    p_sc_allocated_valid_user, p_user <-- allocate_user_if_authenticated (p_endpoint_config_idx, p_user_token, p_sc_valid_user) =
    PRE
        p_user_token : t_user_token_i &
        p_user_token : t_user_token &
        p_endpoint_config_idx : t_endpoint_config_idx_i &
        p_endpoint_config_idx : t_endpoint_config_idx &
        p_sc_valid_user : t_StatusCode_i &
        p_sc_valid_user : t_StatusCode
    THEN
        IF p_sc_valid_user = e_sc_ok
        THEN
            CHOICE
                p_sc_allocated_valid_user := p_sc_valid_user ||
                p_user :(p_user : t_user_i & p_user : t_user)
            OR
                p_sc_allocated_valid_user := e_sc_bad_out_of_memory ||
                p_user := c_user_indet
            END
        ELSE
            p_sc_allocated_valid_user := p_sc_valid_user ||
            p_user := c_user_indet
        END
    END

OPERATIONS

    p_sc_allocated_valid_user, p_user <-- allocate_user_if_authenticated (p_endpoint_config_idx, p_user_token, p_sc_valid_user) =
    VAR
        l_is_user_allocated
    IN
        IF p_sc_valid_user = e_sc_ok
        THEN
            l_is_user_allocated, p_user <-- allocate_authenticated_user (p_endpoint_config_idx, p_user_token);
            IF l_is_user_allocated = TRUE
            THEN
                p_sc_allocated_valid_user := p_sc_valid_user
            ELSE
                p_user := c_user_indet;
                p_sc_allocated_valid_user := e_sc_bad_out_of_memory
            END
        ELSE
            p_sc_allocated_valid_user := p_sc_valid_user;
            p_user := c_user_indet
        END
    END
    ;

    /* END OF LOCAL OPERATIONS */

    p_sc_valid_user, p_user <-- allocate_valid_and_authenticated_user (p_user_token, p_channel_config_idx, p_endpoint_config_idx) =
    VAR
        l_user_token_type,
        l_is_supported_user_token_type,
        l_sc_user_authentication
    IN
        l_user_token_type <-- get_user_token_type_from_token (p_user_token);
        l_is_supported_user_token_type <-- is_user_token_supported (l_user_token_type, p_user_token, p_channel_config_idx, p_endpoint_config_idx);
        IF l_is_supported_user_token_type = TRUE
        THEN
            IF l_user_token_type /= e_userTokenType_anonymous THEN
                l_sc_user_authentication <-- is_valid_user_authentication (p_endpoint_config_idx, l_user_token_type, p_user_token)
            ELSE
                /* It is not necessary to authenticate Anonymous user */
                l_sc_user_authentication := e_sc_ok
            END;
            p_sc_valid_user, p_user <-- allocate_user_if_authenticated (p_endpoint_config_idx, p_user_token, l_sc_user_authentication)
        ELSE
            p_user := c_user_indet;
            p_sc_valid_user := e_sc_bad_identity_token_invalid
        END
    END

END
