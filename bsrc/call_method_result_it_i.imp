/*
 * Licensed to Systerel under one or more contributor license
 * agreements. See the NOTICE file distributed with this work
 * for additional information regarding copyright ownership.
 * Systerel licenses this file to you under the Apache
 * License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License. You may obtain
 * a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

IMPLEMENTATION
    call_method_result_it_i

REFINES
    call_method_result_it

SEES
    constants

CONCRETE_VARIABLES

    currentCallMethodResultIdx_i,
    nb_callMethodResultIdx_max_refs_i

INVARIANT
    currentCallMethodResultIdx_i : NAT &
    nb_callMethodResultIdx_max_refs_i : NAT &

    nb_callMethodResultIdx_max_refs = nb_callMethodResultIdx_max_refs_i &
    currentCallMethodResultIdx_i : 0..nb_callMethodResultIdx_max_refs_i &
    nextCallMethodResultIdx = currentCallMethodResultIdx_i + 1 &

    (nb_callMethodResultIdx_max_refs_i = 0 =>
        callMethodResultIdx_to_iterate = {} &
        callMethodResultIdx_iterated = {}) &

    (nb_callMethodResultIdx_max_refs_i /= 0 =>
        callMethodResultIdx_to_iterate = currentCallMethodResultIdx_i+1..nb_callMethodResultIdx_max_refs_i &
        callMethodResultIdx_iterated = 1..currentCallMethodResultIdx_i)

INITIALISATION
    currentCallMethodResultIdx_i := 0;
    nb_callMethodResultIdx_max_refs_i := 0

OPERATIONS


    p_continue <-- init_iter_callMethodResultIdx(p_nb_callMethodResultIdx_max_refs) =
    BEGIN
        nb_callMethodResultIdx_max_refs_i := p_nb_callMethodResultIdx_max_refs;
        currentCallMethodResultIdx_i := 0;
        p_continue := bool (0 < p_nb_callMethodResultIdx_max_refs)
    END
    ;

    p_continue, p_callMethodResultIdx <-- continue_iter_callMethodResultIdx =
    BEGIN
        currentCallMethodResultIdx_i := currentCallMethodResultIdx_i + 1;
        p_callMethodResultIdx := currentCallMethodResultIdx_i;
        p_continue := bool (currentCallMethodResultIdx_i < nb_callMethodResultIdx_max_refs_i)
    END

END