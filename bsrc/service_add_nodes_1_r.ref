/*
 * Licensed to Systerel under one or more contributor license
 * agreements. See the NOTICE file distributed with this work
 * for additional information regarding copyright ownership.
 * Systerel licenses this file to you under the Apache
 * License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License. You may obtain
 * a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

REFINEMENT
    service_add_nodes_1_r

REFINES
    service_add_nodes_1

SEES
    constants,
    constants_statuscodes_bs,
    message_in_bs,
    message_out_bs

DEFINITIONS
    "address_space.def";

ABSTRACT_VARIABLES
    d_adsp_variables

INVARIANT
    d_adsp_invariant

INITIALISATION
    d_adsp_variables
    :(
        d_adsp_invariant
    )

OPERATIONS

    sc_operation, new_nodeId
      <-- treat_add_nodes_item (p_parentExpNid, p_refTypeId, p_reqExpNodeId, p_browseName, p_nodeClass, p_nodeAttributes, p_typeDefId) =
    PRE
        p_parentExpNid : t_ExpandedNodeId_i &
        p_parentExpNid : t_ExpandedNodeId &
        p_refTypeId : t_NodeId_i &
        p_refTypeId : t_NodeId &
        p_reqExpNodeId : t_ExpandedNodeId_i &
        p_browseName : t_QualifiedName_i &
        p_browseName : t_QualifiedName &
        p_nodeClass : t_NodeClass_i &
        p_nodeClass : t_NodeClass &
        p_nodeAttributes : t_NodeAttributes_i &
        p_nodeAttributes : t_NodeAttributes &
        p_typeDefId : t_ExpandedNodeId_i &
        (p_nodeClass :{e_ncl_Object, e_ncl_Variable} => p_typeDefId : t_ExpandedNodeId)
    THEN
        sc_operation,
        new_nodeId,
        d_adsp_nodes_variables
        :(
            d_adsp_nodes_invariant &
            sc_operation : t_StatusCode_i &
            sc_operation : {e_sc_ok,
                            e_sc_bad_parent_node_id_invalid,
                            e_sc_bad_reference_type_id_invalid,
                            e_sc_bad_reference_not_allowed,
                            e_sc_bad_node_id_rejected,
                            e_sc_bad_node_id_exists,
                            /* bad_node_class_invalid => already checked */
                            /* e_sc_bad_browse_name_invalid => already checked */
                            e_sc_bad_browse_name_duplicated,
                            /* bad_node_attributes_invalid => already checked */
                            e_sc_bad_type_definition_invalid,
                            e_sc_bad_node_attributes_invalid,
                            e_sc_bad_out_of_memory,
                            e_sc_bad_node_class_invalid} & // BadNodeClassInvalid used for unsupported NodeClass for now3
            new_nodeId : t_NodeId_i &
            (sc_operation = e_sc_ok =>
                /* Parent node is valid */
                p_parentExpNid : dom(conv_ExpandedNodeId_NodeId) &
                conv_ExpandedNodeId_NodeId(p_parentExpNid) : ran(a_NodeId) &
                /* Reference type is valid */
                p_refTypeId : Type_IsReferenceTypeIdValid &
                new_nodeId : t_NodeId &
                new_nodeId /: ran(a_NodeId) &
                (p_reqExpNodeId : t_ExpandedNodeId =>
                    p_reqExpNodeId : dom(conv_ExpandedNodeId_NodeId) & new_nodeId = conv_ExpandedNodeId_NodeId(p_reqExpNodeId)) &
                /* Type definition is valid if node is an object or variable */
                (p_nodeClass = e_ncl_Object or p_nodeClass = e_ncl_Variable =>
                    p_typeDefId : t_ExpandedNodeId &
                    p_typeDefId : dom(conv_ExpandedNodeId_NodeId) &
                    conv_ExpandedNodeId_NodeId(p_typeDefId) : ran(a_NodeId)
                )
            ) &
            (not(sc_operation = e_sc_ok) => new_nodeId = c_NodeId_indet) &
            new_nodeId : t_NodeId_i &
            (sc_operation = e_sc_ok =>
                new_nodeId : t_NodeId &
                new_nodeId : ran(a_NodeId) &
                (p_reqExpNodeId : t_ExpandedNodeId =>
                    p_reqExpNodeId : dom(conv_ExpandedNodeId_NodeId) &
                    new_nodeId = conv_ExpandedNodeId_NodeId(p_reqExpNodeId))
            )
        )
    END
END
